{
  config,
  lib,
  ...
}: let
  cfg = config.services.tailscale;
in {
  options = with lib; {
    services.tailscale.persistence = mkOption {
      type = types.attrsOf types.raw;
      default = {
        transient.directories = [
          # taildrops buffered here until `tailscale file get ...` is used
          # (telemetry to upload is also buffered here)
          "/var/lib/tailscale/files"
        ];
        important.files = [
          # random log ID generated by every node
          # used for the telemetry that tailscale uploads
          "/var/lib/tailscale/tailscaled.log.conf"
        ];
        secret.files = [
          # includes the machine key
          "/var/lib/tailscale/tailscaled.state"
        ];

        # Unpersisted files:
        # /var/lib/tailscale/tailscaled.log1.txt: deprecated log file (always empty)
        # /var/lib/tailscale/tailscaled.log2.txt: deprecated log file (always empty)
      };
      description = ''
        Persist tailscale keys, files and ids.
      '';
    };
  };

  config = let
    persistence = builtins.intersectAttrs config.environment.automaticPersistence cfg.persistence;
  in
    lib.mkIf (cfg.enable && persistence != {}) {
      environment.persistence =
        lib.mkMerge
        (lib.mapAttrsToList
          (k: v: {${config.environment.automaticPersistence.${k}.path} = v;})
          persistence);
    };
}
